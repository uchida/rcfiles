# -*- shell-script -*-
# ~/.zshrc: the zsh interactive shell startup file
# about (un)setopt option, see man zshoptions
# about autoload module, see man zshcontrib

# load shell run commands
if [ -s "$HOME/.shell/rc" ]; then
    source "$HOME/.shell/rc"
fi

# use completion
autoload -U compinit; compinit -u -d "$HOME/.shell/cache/zcompdump"
# use color
autoload colors; colors

# do not send HUP signals to background job
setopt no_hup
# do not use automatically menu completion
unsetopt auto_menu
# if a command is not executable and it is a directory, cd to it
setopt auto_cd
# change directory with automatically pushd it
setopt auto_pushd
# expand a filename after =, for example foo=~/bar/...
setopt magic_equal_subst
# expand =filename as a path of filename
setopt equals
# expand valuables in prompt
setopt prompt_subst
# do not append duplicate commands to history
setopt hist_ignore_dups
setopt share_history
# do not append the history (fc -l) command to history
setopt hist_no_store
# reduce unnecessary blanks in command
setopt hist_reduce_blanks
# do not append duplicated directories to history
setopt pushd_ignore_dups

# histroy file setting
HISTFILE="$HOME/.shell/cache/zsh_history"
HISTSIZE=100000
SAVEHIST=100000

# use Emacs keybind
bindkey -e
# C-p/n as history search back/forward
bindkey '^P' history-beginning-search-backward
bindkey '^N' history-beginning-search-forward

# the prompt style: "host:dir user> ", deeper PATH is abbreviated as ...
PROMPT_HOST="%m"
PROMPT_DIR="%(3~,.../%2~,%~)"
# the prompt shows host with red color in the ssh connection
if [ -n "$SSH_CLIENT" ]; then
    PROMPT_HOST="%{$fg[red]%}$PROMPT_HOST%{$reset_color%}"
fi
PROMPT="$PROMPT_HOST:$PROMPT_DIR $USER> "

# show vcs info in RPROMPT
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable svn git hg
zstyle ':vcs_info:*' formats "[%b]"
zstyle ':vcs_info:*' actionformats "[(%F{1}%a%F{7})%b]"
zstyle ':vcs_info:*' stagedstr "S"
zstyle ':vcs_info:*' unstagedstr "U"
zstyle ':vcs_info:svn:*' branchformat "%F{2}%b%F{7}:%r"
zstyle ':vcs_info:git*:*' check-for-changes true
zstyle ':vcs_info:git*:*' formats "%F{1}%c%u%F{7}[%F{2}%b%F{7}]"
zstyle ':vcs_info:git*:*' actionformats "%F{1}%c%u%F{7}[(%F{1}%a%F{7})%F{2}%b%F{7}]"
zstyle ':vcs_info:hg:*' check-for-changes true
zstyle ':vcs_info:hg:*' get-revision true
zstyle ':vcs_info:hg:*' get-bookmarks true
zstyle ':vcs_info:hg:*' formats '%F{1}%u%F{7}[%b:%F{2}%m%F{7}]'
zstyle ':vcs_info:hg:*' actionformats "%F{1}%u%F{7}[(%F{1}%a%F{7})%b:%F{2}%m%F{7}]"
zstyle ':vcs_info:hg:*' branchformat "%F{2}%b%F{7}:%r"
zstyle ':vcs_info:hg:*' hgrevformat "%r"
update_vcs_info () {
    vcs_info
}
RPROMPT='${vcs_info_msg_0_}'
autoload -Uz add-zsh-hook
add-zsh-hook precmd update_vcs_info

# for z.sh
if [ -x "$(which _z)" ]; then
    function zaddpwd() {
        _z --add "$(pwd -P)"
    }
    add-zsh-hook precmd zaddpwd
fi

# load machine specific settings
if [ -f "$HOME/.shell/zshrc-local" ]; then
    source "$HOME/.shell/zshrc-local"
fi
